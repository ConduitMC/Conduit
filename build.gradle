plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "checkstyle"
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://libraries.minecraft.net/"
    }
    maven {
        url = "https://repo.spongepowered.org/maven/"
    }
    maven {
        url = "https://repo.conduit.systems/repository/releases/"
    }
}

dependencies {
    api "org.projectlombok:lombok:1.18.10"
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    api "net.minecraft:server:1.14.4"
    api "org.spongepowered:mixin:0.8-SNAPSHOT"

    api "org.reflections:reflections:0.9.11"
    api "com.fasterxml.jackson.core:jackson-databind:2.10.0"
    api "org.jline:jline:3.13.0"
}

group = theGroup
version = theVersion
sourceCompatibility = "1.8"
targetCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

checkstyleMain {
    configFile = file("${rootDir}/checkstyle.xml")
}

task install(dependsOn: [publishToMavenLocal])

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                "MixinConnector" : "systems.conduit.main.mixin.MixinConnector"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username System.getProperty("MAVEN_USER")?:'ci'
                password System.getProperty("MAVEN_PASSWORD")?:'password'
            }
            url "https://repo.conduit.systems/repository/releases/"
        }
    }
}
